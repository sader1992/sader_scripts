//===== rAthena Script =======================================
//= Guild Storage with Guild Logs
//===== By: ==================================================
//= Sader1992
//= Free!!
// https://rathena.org/board/profile/30766-sader1992/
//===== Current Version: =====================================
//= 1.0
//===== Compatible With: ===================================== 
//= rAthena Project
// https://github.com/sader1992/sader_scripts
// https://github.com/rathena/rathena/commit/55acdb9863382d8935d9df25e1462d5d1ebd7d54#diff-ae5800534bb25effa37150c6874eb4ad
// AND ABOVE! ONLY.
//===== Description: =========================================
// This allow the players to check the Guild Storage Log.
// Player must have guild storage permission to check the log.
//============================================================
//============================================================
prontera,158,178,8	script	Guild Storage	10129,{
	switch(select("open guild storage:guild storage log")){
		case 1:
			switch(guildopenstorage()){
				case GSTORAGE_OPEN: break;
				case GSTORAGE_STORAGE_ALREADY_OPEN: mes "you need to close the player storage first"; break;
				case GSTORAGE_ALREADY_OPEN: mes "guild storage is already open"; break;
				case GSTORAGE_NO_GUILD: mes "you are not in guild"; break;
				case GSTORAGE_NO_STORAGE: mes "the guild didn't open the guild storage skill"; break;
				case GSTORAGE_NO_PERMISSION: mes "your guild master didn't give you a guild storage permission"; break;
			}
			break;
		case 2:
			if(!getcharid(2)){
				mes "you don't have guild";
				close;
			}
			if(!guild_has_permission(GUILD_PERM_STORAGE)){
				mes "you don't have permission for the guild storage";
				close;
			}
			query_sql("SELECT `time`,`name`,`nameid`,`amount` FROM `guild_storage_log` WHERE `guild_id` = '" + getcharid(2) + "' order by `id` DESC", .@time$ ,.@name$,.@item_id,.@amount);
			if(!.@item_id){
				mes "there is no log yet";
				close;
			}
			dispbottom "===================================================",0xFFD64F;
			dispbottom "=================== Guild Storage Log ====================",0xFFD64F;
			dispbottom "===================================================",0xFFD64F;
			for(.@i=0;.@i<getarraysize(.@item_id);.@i++)
				dispbottom .@time$[.@i] + " Player[" + .@name$[.@i] + "] Item Name[" + getitemname(.@item_id[.@i]) + "] Item Amount[" + .@amount[.@i] + "]",((.@amount[.@i] > 0)?0x2300FF:0xFF0000);
			dispbottom "===================================================",0xFFD64F;
	}
end;
}